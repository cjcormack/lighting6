//Use common build configuration for modules
apply from: '../../gradle/common/common-module-build.gradle'

// Uncomment if not using Kotlin globally
//apply from: '../../gradle/common/common-kotlin-build.gradle'

// CC Note: Would be nice to be able to put this in 'common-nk-build.gradle', but doing so doesn't appear to work.
buildscript {
	apply from: '../../gradle/common/common-nk-boilerplate.gradle', to: buildscript
}

buildscript {
	dependencies {
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.18'
	}
}

apply plugin: 'com.google.protobuf'

//Additional Compile Dependencies
dependencies {
	implementation "io.grpc:grpc-netty-shaded:1.27.2"
	implementation "io.grpc:grpc-protobuf:1.27.2"
	implementation "io.grpc:grpc-stub:1.27.2"

	compileOnly "javax.annotation:javax.annotation-api:1.2"

	compileOnly group:'urn.org.netkernel', name: 'mod.hds', version: '[0.5.0,)'

	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"){
		exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
		exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-common'
	}
}

sourceSets["main"].proto.srcDirs("src")

protobuf {
	protoc { artifact = 'com.google.protobuf:protoc:3.11.0' }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.27.2" }
	}
	generateProtoTasks {
		all()*.plugins { grpc {} }
	}
}
